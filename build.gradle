buildscript {
    ext {
        targetAndroidSdk = 33
        minAndroidSdk = 26

        Modules = [
                Data: ":data",
                Core: [
                        Strings: ":core:strings",
                        Ui: ":core:ui",
                        Common: ":core:common",
                        Theme: ":core:theme",
                ],
                Feature: [
                        Recognition: ":feature:recognition",
                        RecognitionQueue: ":feature:recognition-queue",
                        Library: ":feature:library",
                        Preferences: ":feature:preferences",
                        Track: ":feature:track",
                        Onboarding: ":feature:onboarding",
                        DeveloperMode: ":feature:developer-mode",
                ]
        ]
    }
    repositories {
        google()
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false

    alias libs.plugins.ksp apply false
    alias libs.plugins.kapt apply false
    alias libs.plugins.hilt apply false
    alias libs.plugins.protobuf apply false
}

//gradle 8.0 uses jdk 17 while kapt requires same as jvmTarget (1.8 in this project)
//https://youtrack.jetbrains.com/issue/KT-55947
allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}