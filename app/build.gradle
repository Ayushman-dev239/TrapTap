plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'

}

android {
    namespace 'com.mrsep.musicrecognizer'
    compileSdk targetAndroidSdk

    defaultConfig {
        applicationId "com.mrsep.musicrecognizer"
        minSdk minAndroidSdk
        targetSdk targetAndroidSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').canRead()) {
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
        }
        buildConfigField 'String', 'AUDD_TOKEN', properties.getProperty("api.audd.token")
        buildConfigField "boolean", "LOG_DEBUG_MODE", "false"
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG_MODE", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOG_DEBUG_MODE", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'

        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                        project.buildDir.absolutePath + "/compose_metrics"
        ]
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                        project.buildDir.absolutePath + "/compose_metrics"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            resources.excludes += "DebugProbesKt.bin"
        }
    }
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

dependencies {

    implementation platform(Libs.Compose.Bom)
    androidTestImplementation platform(Libs.Compose.Bom)

    implementation Libs.Compose.Material3
    implementation Libs.Compose.ToolingPreview
    debugImplementation Libs.Compose.DebugTooling
    androidTestImplementation Libs.Compose.AndroidTestJUnit
    debugImplementation Libs.Compose.DebugManifest

    implementation Libs.Compose.Navigation
    implementation Libs.Compose.HiltNavigation

    implementation Libs.Android.CoreKtx
    implementation Libs.Android.LifecycleRuntime
    implementation Libs.Android.ActivityCompose
    implementation Libs.Android.LifecycleRuntimeCompose
    implementation Libs.Android.LifecycleViewModelCompose
    implementation Libs.Android.SplashScreenCore

    testImplementation Libs.Test.JUnit
    androidTestImplementation Libs.AndroidTest.JUnitExt
    androidTestImplementation Libs.AndroidTest.EspressoCore

    implementation Libs.Accompanist.SystemUiController

    implementation Libs.JetBrains.CoroutinesAndroid
    implementation Libs.JetBrains.CollectionImmutable

    implementation Libs.DataStore.Core // FIXME should be deleted when proto model will be replaced by data model in module data
    implementation Libs.DataStore.ProtobufJavalite

    implementation Libs.Hilt.Core
    kapt Libs.Hilt.Kapt

    implementation Libs.Coil.Core
    implementation Libs.Paging3.Common

    implementation Libs.WorkManager.RuntimeKtx
    implementation Libs.WorkManager.HiltWorkManager
    kapt Libs.WorkManager.Kapt



    implementation project(Modules.Data)
    implementation project(Modules.Core.Ui)
    implementation project(Modules.Core.Strings)
    implementation project(Modules.Core.Common)
    implementation project(Modules.Feature.Library)
    implementation project(Modules.Feature.Track)
    implementation project(Modules.Feature.Recognition)
    implementation project(Modules.Feature.RecognitionQueue)
    implementation project(Modules.Feature.Preferences)
    implementation project(Modules.Feature.Onboarding)
    implementation project(Modules.Feature.DeveloperMode)

}
